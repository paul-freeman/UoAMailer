buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "nz.ac.auckland.lablet.mailer"
        minSdkVersion 17
        targetSdkVersion 18
        versionCode 3
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('keystore.properties').newDataInputStream())

        release {
            storeFile file(properties.getProperty('localStoreFile'))
            storePassword properties.getProperty('localStorePassword')
            keyAlias properties.getProperty('localKeyAlias')
            keyPassword properties.getProperty('localKeyPassword')
        }
    }

    buildTypes {
        debug {
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        uoa_private {
        }
        all_public {
        }
    }
}

configurations {
    doclet
}

dependencies {
    doclet group: 'org.jboss.apiviz', name: 'apiviz', version: '1.3.2.GA'
}

android.applicationVariants.all { variant ->
    task("generate${variant.name}Javadoc", type: Javadoc) {
        source = variant.javaCompile.source
        def androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"

        classpath = files(variant.javaCompile.classpath.files, androidJar)
        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
            doclet 'org.jboss.apiviz.APIviz'
            docletpath file(configurations.doclet.asPath)
        }
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile "com.google.code.gson:gson:2.7"
    compile "com.netflix.rxjava:rxjava-android:0.13.2"
    compile "com.netflix.rxjava:rxjava-core:0.14.7"
    compile "com.android.support:support-annotations:25.2.0"
    compile "com.android.support:support-v4:25.2.0"
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
dependencies {
    compile 'com.android.support:appcompat-v7:25.2.0'
}
